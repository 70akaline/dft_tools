SET(PythonBuildExecutable ${CMAKE_BINARY_DIR}/build_pytriqs)

# runs a c++ test
# if there is a .output file a comparison test is done
# Example: add_cpp_test(my_code)
#   where my_code is the cpp executable my_code.output is the expected output
macro(triqs_add_cpp_test testname)
 enable_testing()

 if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output)

  file( COPY ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output DESTINATION ${CMAKE_CURRENT_BINARY_DIR})

  add_test(${testname}
   ${CMAKE_COMMAND}
   -Dname=${testname}${ARGN}
   -Dcmd=${CMAKE_CURRENT_BINARY_DIR}/${testname}${ARGN}
   -Dreference=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output
   -P @CMAKE_INSTALL_PREFIX@/share/triqs/cmake/run_test.cmake
  )

 else (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output)

  add_test(${testname}${ARGN} ${testname}${ARGN})

 endif (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output)

endmacro(triqs_add_cpp_test)
 
# runs a python test
# if there is a .output file a comparison test is done
# Example: add_python_test(my_script)
#   where my_script.py is the script and my_script.output is the expected output
macro(triqs_add_python_test testname)
 enable_testing()

 if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output)

  file( COPY ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output DESTINATION ${CMAKE_CURRENT_BINARY_DIR})

  add_test(${testname}
   ${CMAKE_COMMAND}
   -Dname=${testname}
   -Dcmd=${PythonBuildExecutable}
   -Dinput=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.py
   -Dreference=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output
   -P @CMAKE_INSTALL_PREFIX@/share/triqs/cmake/run_test.cmake
  )

 else (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output)

  add_test(${testname}
   ${CMAKE_COMMAND}
   -Dname=${testname}
   -Dcmd=${PythonBuildExecutable}
   -Dinput=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.py
   -P @CMAKE_INSTALL_PREFIX@/share/triqs/cmake/run_test.cmake
  )

 endif (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.output)

endmacro(triqs_add_python_test)
