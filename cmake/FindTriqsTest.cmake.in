SET(PythonBuildExecutable ${CMAKE_BINARY_DIR}/build_pytriqs)

# runs a c++ test
# if there is a .ref file a comparison test is done
# Example: add_cpp_test(my_code)
#   where my_code is the cpp executable my_code.ref is the expected output
macro(triqs_add_cpp_test testname)
 enable_testing()

 if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref)

  file(COPY ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref DESTINATION ${CMAKE_CURRENT_BINARY_DIR})

  add_test(${testname}
   ${CMAKE_COMMAND}
   -Dname=${testname}${ARGN}
   -Dcmd=${CMAKE_CURRENT_BINARY_DIR}/${testname}${ARGN}
   -Dreference=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref
   -P @CMAKE_INSTALL_PREFIX@/share/triqs/cmake/run_test.cmake
  )

 else (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref)

  add_test(${testname}${ARGN} ${testname}${ARGN})

 endif (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref)

endmacro(triqs_add_cpp_test)
 
# runs a python test
# if there is a .ref file a comparison test is done
# Example: add_python_test(my_script)
#   where my_script.py is the script and my_script.ref is the expected output
macro(triqs_add_python_test testname)
 enable_testing()

 if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref)

  file( COPY ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref DESTINATION ${CMAKE_CURRENT_BINARY_DIR})

  add_test(${testname}
   ${CMAKE_COMMAND}
   -Dname=${testname}
   -Dcmd=${PythonBuildExecutable}
   -Dinput=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.py
   -Dreference=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref
   -P @CMAKE_INSTALL_PREFIX@/share/triqs/cmake/run_test.cmake
  )

 else (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref)

  add_test(${testname}
   ${CMAKE_COMMAND}
   -Dname=${testname}
   -Dcmd=${PythonBuildExecutable}
   -Dinput=${CMAKE_CURRENT_SOURCE_DIR}/${testname}.py
   -P @CMAKE_INSTALL_PREFIX@/share/triqs/cmake/run_test.cmake
  )

 endif (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${testname}.ref)

endmacro(triqs_add_python_test)


# add all the cpp tests in the current directory
macro(triqs_add_all_cpp_tests_in_current_dir)
 FILE(GLOB TestList RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} *.cpp)
 FOREACH(TestName1 ${TestList})
  STRING(REPLACE ".cpp" "" TestName ${TestName1})
  add_executable(${TestName} ${CMAKE_CURRENT_SOURCE_DIR}/${TestName}.cpp)
  triqs_set_rpath_for_target(${TestName})
  triqs_add_cpp_test(${TestName})
  if(TESTS_C_WITH_VALGRIND)
   add_test(${TestName}_valgrind valgrind --error-exitcode=1 ${CMAKE_CURRENT_BINARY_DIR}/${TestName})
  endif()
 ENDFOREACH(TestName1 ${TestList})
endmacro()

